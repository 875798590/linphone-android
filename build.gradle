// Project information
buildDir = 'bin'

def getPackageName() {
    return "org.linphone"
}

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath 'com.google.gms:google-services:3.1.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}


apply plugin: 'com.android.application'

///// Exclude Files /////

def excludeFiles = []
// Remove or comment if you want to use those
excludeFiles.add('src/android/org/linphone/tutorials/*.java')
excludeFiles.add('**/gcm*')

def excludePackage = []

excludePackage.add('**/gdb.*')
excludePackage.add('**/libopenh264**')
excludePackage.add('**/**tester**')
excludePackage.add('**/LICENSE.txt')

/////////////////////////

repositories {
    maven {
        // Replace snapshots by releases for releases !
        url "https://linphone.org/snapshots/maven_repository"
    }
}

android {
    defaultConfig {
        compileSdkVersion 26
        applicationId getPackageName()
        multiDexEnabled true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'MissingTranslation', 'UnusedResources'
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "linphone-android-${variant.buildType.name}-${project.version}.apk"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            def srcDirs = ['src/android']
            java.srcDirs = srcDirs
            resources.srcDirs = srcDirs
            aidl.srcDirs = srcDirs
            renderscript.srcDirs = srcDirs
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']

            java.excludes = excludeFiles

            packagingOptions {
                excludes = excludePackage
            }
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    packagingOptions {
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LICENSE'
        exclude 'META-INF/MANIFEST.MF'
    }
}

dependencies {
    implementation 'org.apache.commons:commons-compress:1.16.1'
    implementation 'com.google.firebase:firebase-messaging:15.0.2'
    implementation 'com.android.support:support-v4:27.0.1'
    releaseImplementation project(path: ":linphone-sdk-android", configuration: 'release')
    debugImplementation project(path: ":linphone-sdk-android", configuration: 'debug')
}

apply plugin: 'com.google.gms.google-services'